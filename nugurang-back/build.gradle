plugins {
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id "io.freefair.lombok" version "5.2.1"
    id 'java'
    id 'java-library'
    id 'org.springframework.boot' version '2.3.4.RELEASE'
    id 'org.liquibase.gradle' version '2.0.4'
    id 'pmd'
}

def checkstyleVersion = '8.+'
def pmdVersion = '6.+'

group = 'com.nugurang'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '14'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    checkstyleConfig
}

repositories {
    mavenCentral()
}

diff.dependsOn compileJava
diffChangeLog.dependsOn compileJava
generateChangelog.dependsOn compileJava

dependencies {
    api(
        'com.querydsl:querydsl-jpa:4.+'
    )

    annotationProcessor(
        'com.querydsl:querydsl-apt::jpa',
        'javax.persistence:javax.persistence-api',
        'javax.annotation:javax.annotation-api',
        'org.springframework.boot:spring-boot-configuration-processor',
        'org.projectlombok:lombok'
    )

    checkstyleConfig("com.puppycrawl.tools:checkstyle:${checkstyleVersion}") {
        transitive = false
    }

    compileOnly(
        'org.projectlombok:lombok'
    )

    developmentOnly(
        'org.springframework.boot:spring-boot-devtools'
    )

    implementation(
        'com.google.guava:guava:29.+',
        'com.graphql-java-kickstart:graphql-spring-boot-starter:7.+',
        'com.graphql-java-kickstart:voyager-spring-boot-starter:7.+',
        'com.graphql-java:graphql-java-extended-scalars:1.+',
        'com.graphql-java:graphql-java-spring-boot-starter-webmvc:1.+',
        'com.vladmihalcea:hibernate-types-52:2.+',
        'org.springframework.boot:spring-boot-starter-actuator',
        'org.springframework.boot:spring-boot-starter-data-jpa',
        'org.springframework.boot:spring-boot-starter-oauth2-client',
        'org.springframework.boot:spring-boot-starter-security',
        'org.springframework.boot:spring-boot-starter-validation',
        'org.springframework.boot:spring-boot-starter-web',
    )

    liquibaseRuntime(
        'org.liquibase:liquibase-core',
        'org.liquibase.ext:liquibase-hibernate5:3.+'
    )

    runtimeOnly(
        'com.graphql-java-kickstart:graphiql-spring-boot-starter:7.0.1',
        'com.h2database:h2',
        'mysql:mysql-connector-java'
    )

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    testImplementation(
        'com.graphql-java-kickstart:graphql-spring-boot-starter-test:7.0.1',
        'org.springframework.security:spring-security-test'
    )
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
}

checkstyle {
    toolVersion = checkstyleVersion
    showViolations = true
    ignoreFailures = false
    maxWarnings = 0
    maxErrors = 0
}

checkstyleMain {
    logging.setLevel(LogLevel.LIFECYCLE)
}

checkstyleTest {
    enabled = false
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

pmd {
    toolVersion = pmdVersion
    consoleOutput = true
    ignoreFailures = true
    reportsDir = file("${buildDir}/reports/pmd")
}
 
tasks.withType(Pmd) {
    reports {
        xml.enabled false
        html.enabled true
    }
}
